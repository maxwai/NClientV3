apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        release {
            keyAlias = keystoreProperties['keyAlias']
            keyPassword = keystoreProperties['keyPassword']
            storeFile = file(keystoreProperties['storeFile'])
            storePassword = keystoreProperties['storePassword']
        }
    }
    compileSdk = 36
    defaultConfig {
        applicationId = "com.maxwai.nclientv3"
        // Format: MmPPbb
        // M: Major, m: minor, P: Patch, b: build
        versionCode = 410002
        multiDexEnabled = true
        versionName = "4.1.0_pre3"
        vectorDrawables.useSupportLibrary = true
        proguardFiles 'proguard-rules.pro'
    }
    flavorDimensions = ["sdk"]
    productFlavors {
        post28 {
            dimension = "sdk"
            targetSdk = 36
            minSdkVersion 28
        }
        pre28 {
            dimension = "sdk"
            targetSdk = 28
            minSdkVersion 26
        }
    }
    buildTypes {
        release {
            minifyEnabled = true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
            versionNameSuffix = "-release"
            resValue "string", "app_name", "NClientV3"
            signingConfig = signingConfigs.release
        }
        debug {
            applicationIdSuffix = ".debug"
            versionNameSuffix = "-debug"
            resValue "string", "app_name", "NClientV3 Debug"
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
    lint {
        abortOnError = false
        checkReleaseBuilds = false
        disable 'RestrictedApi'
    }
    bundle {
        language {
            // Specifies that the app bundle should not support
            // configuration APKs for language resources. These
            // resources are instead packaged with each base and
            // dynamic feature APK.
            enableSplit = false
        }
    }
    namespace = 'com.maxwai.nclientv3'
    buildFeatures {
        buildConfig  = true
    }
    androidResources {
        generateLocaleConfig = true
    }
}

android.applicationVariants.configureEach { variant ->
    variant.outputs.configureEach {
        def suffix = ""
        if (variant.flavorName == "pre28") {
            suffix = "_pre28"
        }
        def version = variant.versionName.substring(0, variant.versionName.lastIndexOf("-"))
        outputFileName = "NClientV3_${version}${suffix}.apk"
    }
}

dependencies {
// Android
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.fragment:fragment:1.8.9'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.viewpager2:viewpager2:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.work:work-runtime:2.11.0'
    implementation "androidx.core:core-splashscreen:1.0.1"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.google.android.material:material:1.13.0'

// Other
    // image loading and caching
    implementation('com.github.bumptech.glide:glide:5.0.5') {
        exclude group: "com.android.support"
    }
    implementation 'com.github.bumptech.glide:okhttp3-integration:5.0.5@aar'
    annotationProcessor 'com.github.bumptech.glide:compiler:5.0.5'
    // For Http Connection
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:5.2.1'
    // Used to store the cookies between runs
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'
    // To parse HTML
    implementation 'org.jsoup:jsoup:1.21.2'
    // color picker
    implementation 'com.github.yukuku:ambilwarna:2.0.1'
    // fast scroll
    implementation 'me.zhanghai.android.fastscroll:library:1.3.0'
}
